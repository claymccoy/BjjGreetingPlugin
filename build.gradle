buildscript {
  ext.kotlin_version = '1.3.50'

  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

dependencies {
  compileOnly (group: 'greeting', name: 'api', version: '1.0.0')
  compileOnly(group: 'org.pf4j', name: 'pf4j', version: "${pf4jVersion}") {
    exclude group: "org.slf4j"
  }
  kapt(group: 'org.pf4j', name: 'pf4j', version: "${pf4jVersion}")
  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
  compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

  testImplementation (group: 'greeting', name: 'api', version: '1.0.0')
  testImplementation "org.junit.jupiter:junit-jupiter-api:5.3.2"
  testImplementation group: 'io.strikt', name: 'strikt-core', version: '0.22.1'
  testImplementation group: 'dev.minutest', name: 'minutest', version: '1.10.0'

  testRuntime "org.junit.jupiter:junit-jupiter-engine:5.3.2"
  testRuntime "org.junit.platform:junit-platform-launcher:1.3.2"
}

jar {
  manifest {
    attributes 'Plugin-Class': "${pluginClass}",
          'Plugin-Id': "${pluginId}",
          'Plugin-Version': "${version}",
          'Plugin-Provider': "${pluginProvider}"
  }
}

tasks {
  test {
    useJUnitPlatform()
    afterTest { descriptor, result ->
      // work around a bug in Gradle versions before 6.1, see https://github.com/junit-team/junit5/issues/2041
      println "\n$descriptor.className [$descriptor.classDisplayName] > $descriptor.name [$descriptor.displayName]: $result.resultType"
    }
  }
}

task plugin(type: Jar) {
  baseName = "${pluginId}"
  into('classes') {
    with jar
  }
  into('lib') {
    from configurations.compile
  }
  extension('zip')
}

task assemblePlugin(type: Copy) {
  from plugin
  into rootProject.buildDir.path
}

build.dependsOn project.tasks.assemblePlugin
